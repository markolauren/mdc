
resourcecontainers
| where type == "microsoft.resources/subscriptions"
| project subscriptionId, subscriptionName = name
| join kind=inner (
    securityresources
    | where type =~ "microsoft.security/pricings"
    | extend pricingTier = iff(properties.pricingTier == "Free", "OFF", "on"), subPlan = properties.subPlan
    | extend planSet = pack(name, level = case(isnotempty(subPlan), subPlan, pricingTier))
    | summarize defenderPlans = make_bag(planSet) by subscriptionId
    | join kind=leftouter (
        resources
        | summarize 
            cloudPostureBillableCount = countif(type == "microsoft.compute/virtualmachines")
                                      + countif(type == "microsoft.compute/virtualmachinescalesets/virtualmachines")
                                      + countif(type == "microsoft.classiccompute/virtualmachines")
                                      + countif(type == "microsoft.storage/storageaccounts")
                                      + countif(type == "microsoft.sql/servers")
                                      + countif(type == "microsoft.sql/managedinstances")
                                      + countif(type == "microsoft.dbforpostgresql/flexibleservers")
                                      + countif(type == "microsoft.dbforpostgresql/servers")
                                      + countif(type == "microsoft.dbformysql/flexibleservers")
                                      + countif(type == "microsoft.dbformysql/servers")
                                      + countif(type == "microsoft.dbformariadb/servers")
                                      + countif(type == "microsoft.synapse/workspaces"),
            vmCount = countif(type == "microsoft.compute/virtualmachines") 
                    + countif(type == "microsoft.hybridcompute/machines")                             
                    + countif(type == "microsoft.compute/virtualmachinescalesets/virtualmachines")
                    + countif(type == "microsoft.classiccompute/virtualmachines"),
            appServiceCount = countif(type == "microsoft.web/sites" and kind has "app"),
            sqlServerCount = countif(type == "microsoft.sql/servers")
                           + countif(type == "microsoft.sql/managedinstances"),
            sqlVMCount = countif(type == "microsoft.sqlvirtualmachine/sqlvirtualmachines") 
                       + countif(type == "microsoft.azurearcdata/sqlserverinstances"),
            openSourceDBCount = countif(type == "microsoft.dbforpostgresql/servers")
                              + countif(type == "microsoft.dbforpostgresql/flexibleservers")
                              + countif(type == "microsoft.dbformysql/servers")
                              + countif(type == "microsoft.dbformysql/flexibleservers")
                              + countif(type == "microsoft.dbformariadb/servers"),
            cosmosCount = countif(type == "microsoft.documentdb/databaseaccounts"),
            storageCount = countif(type == "microsoft.storage/storageaccounts"),
            aksCount = countif(type == "microsoft.containerservice/managedclusters"),
            acrCount = countif(type == "microsoft.containerregistry/registries"),
            keyVaultCount = countif(type == "microsoft.keyvault/vaults"),
            armCount = 1,
            aiCount = countif(type == "microsoft.cognitiveservices/accounts"),
            //        + countif(type == "microsoft.aifoundry/projects"),
            //        + countif(type == "microsoft.machinelearningservices/workspaces"),
            //        + countif(type == "microsoft.search/searchservices"),
            apiManagementCount = countif(type == "microsoft.apimanagement/service")
        by subscriptionId
    ) on subscriptionId
) on subscriptionId
| extend
    assetsDefended = 
	    iif(defenderPlans.CloudPosture != "OFF", cloudPostureBillableCount, 0) +
	    iif(defenderPlans.VirtualMachines != "OFF", vmCount, 0) +
	    iif(defenderPlans.AppServices != "OFF", appServiceCount, 0) +
	    iif(defenderPlans.SqlServers != "OFF", sqlServerCount, 0) +
	    iif(defenderPlans.SqlServerVirtualMachines != "OFF", sqlVMCount, 0) +
	    iif(defenderPlans.OpenSourceRelationalDatabases != "OFF", openSourceDBCount, 0) +
	    iif(defenderPlans.CosmosDbs != "OFF", cosmosCount, 0) +
	    iif(defenderPlans.StorageAccounts != "OFF", storageCount, 0) +
	    iif(defenderPlans.Containers != "OFF", aksCount + acrCount, 0) +
	    iif(defenderPlans.KeyVaults != "OFF", keyVaultCount, 0) +
	    iif(defenderPlans.Arm != "OFF", armCount, 0) +
	    iif(defenderPlans.AI != "OFF", aiCount, 0) +
	    iif(defenderPlans.Api != "OFF", apiManagementCount, 0),
	assetsUNdefended = 
	    iif(defenderPlans.CloudPosture == "OFF", cloudPostureBillableCount, 0) +
	    iif(defenderPlans.VirtualMachines == "OFF", vmCount, 0) +
	    iif(defenderPlans.AppServices == "OFF", appServiceCount, 0) +
	    iif(defenderPlans.SqlServers == "OFF", sqlServerCount, 0) +
	    iif(defenderPlans.SqlServerVirtualMachines == "OFF", sqlVMCount, 0) +
	    iif(defenderPlans.OpenSourceRelationalDatabases == "OFF", openSourceDBCount, 0) +
	    iif(defenderPlans.CosmosDbs == "OFF", cosmosCount, 0) +
	    iif(defenderPlans.StorageAccounts == "OFF", storageCount, 0) +
	    iif(defenderPlans.Containers == "OFF", aksCount + acrCount, 0) +
	    iif(defenderPlans.KeyVaults == "OFF", keyVaultCount, 0) +
	    iif(defenderPlans.Arm == "OFF", armCount, 0) +
	    iif(defenderPlans.AI == "OFF", aiCount, 0) +
	    iif(defenderPlans.Api == "OFF", apiManagementCount, 0)
    | project subscriptionName, subscriptionId, 
    assetsDefended,
    assetsUNdefended,
    CloudPosture = defenderPlans.CloudPosture,
    CSPMCount = cloudPostureBillableCount,
    VirtualMachines = defenderPlans.VirtualMachines,
    ServersCount = vmCount,
    AppServices = defenderPlans.AppServices,
    AppServiceCount = appServiceCount,
    SqlServers = defenderPlans.SqlServers,
    AzureSQLCount = sqlServerCount,
    SqlServerVirtualMachines = defenderPlans.SqlServerVirtualMachines,
    SQLVMCount = sqlVMCount,
    OpenSourceRelationalDatabases = defenderPlans.OpenSourceRelationalDatabases,
    OSSdbCount = openSourceDBCount,
    CosmosDB = defenderPlans.CosmosDbs,
    CosmosDatabaseCount = cosmosCount,
    StorageAccounts = defenderPlans.StorageAccounts,
    StorageCount = storageCount,
    Containers = defenderPlans.Containers,
    AKSCount = aksCount,
    ACRCount = acrCount,
    KeyVaults = defenderPlans.KeyVaults,
    KeyVaultCount = keyVaultCount,
    AI = defenderPlans.AI,
    AICount = aiCount,
    API = defenderPlans.Api,
    APIMCount = apiManagementCount,
    Arm = defenderPlans.Arm,
    DNS = defenderPlans.Dns,
    KubernetesService = defenderPlans.KubernetesService,
    ContainerRegistry = defenderPlans.ContainerRegistry
